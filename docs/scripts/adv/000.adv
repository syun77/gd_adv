$1 = 2
$2 += 3
$3 = $1 * $2
%1 = true
if(%1) {
	"%1 は true"
}

"まくらのそうし ―さうし 【枕草子】/"
call hoge
"随筆。三巻。清少納言作。"
"一〇世紀末から一一世紀初頭の成立/"
call piyo
"一条天皇の中宮（のち皇后）定子に"
"出仕した作者の宮廷生活の回想・見聞/"
call momo
def hoge {
	"ほげです/"
}
def piyo {
	"ぴよです/"
}
def momo {
	"ももです/"
}



// ===================================
// イベントコマンドテスト用スクリプト
// ===================================
// 色々な項目テスト
def check_to_something
{
	// 背景表示
	DRB(1)

	while(true) {
		select("確認する項目を選んでください")
		{"画像描画"
			call image
		}
		{"テキスト"
			call text
		}
		{"サウンド"
			call sound
		}
		{"その他"
			call other
		}
		{"戻る"
			break
		}
	}
	"おしまい/"
}

// -----------------------------------
// テキスト関連
// -----------------------------------
def text
{
	while(true) {
		select("確認するテキストの機能を選んでください")
		{"クリック待ちテスト"
			"クリック待ちテキスト１@"
			"クリック待ちテキスト２@"
			"改ページします/"

			"クリック待ちテキスト１"
			"クリック待ちテキスト２@"
			"改ページします/"
		}
		{"メッセージ色変え"
			"つまり……、"
			"この事件は<color.red>密室殺人<color>ということだ/"
			
			"aquaは<color.aqua>水色<color>"
			"blackは<color.black>黒色<color>"
			"blueは<color.blue>青色<color>/"
			
			"grayは<color.gray>灰色<color>"
			"limeは<color.lime>黄緑色<color>"
			"yellowは<color.yellow>黄色<color>/"
		}
		{"ノベルモード"
			WND_CLOSE()
			WAIT(30)
			MSG_MODE(MSG_NOVEL)
			"『中三女子転落死、"
			"いじめの事実は確認されず』/"

			"『今月十四日、市立＊＊中学の屋上から"
			"転落死した葉月あやめさん（十五）。@"
			" "
			"輝かしい未来を前にして、"
			"なぜ自ら命を絶ってしまったのか。@"
			" "
			"あやめさんの死といじめの関連性が指摘されたことから、"
			"＊＊中学の田中校長は全校生徒にアンケートを実施。@"
			" "
			"十六日午後二時に会見を行い、"
			"アンケートの結果いじめの事実は"
			"確認されなかったとして"
			"事件との関連性を否定した。』/"

			WND_CLOSE()
			WAIT(30)

			MSG_MODE(MSG_TALK)
		}
		{"戻る"
			break
		}
	}
	return
}

// -----------------------------------
// 画像関連
// -----------------------------------
def image
{
	while(true) {
		select("確認する画像の機能を選んでください")
		{"立ち絵・顔ウィンドウ"
			while(true) {
				select("確認するキャラクターを"
				"選んでください")
				{"如月あゆみ"
					DRC(1, 2, EF_SLIDE)
					"[如月あゆみ:1]通常 (1)/"
				}
				{"小野宮紅華"
					DRC(101, 2, EF_SLIDE)
					"[小野宮紅華:101]通常（1）/"
				}
				{"名瀬尚人"
					DRC(201, 2, EF_SLIDE)
					"[名瀬尚人:201]通常（1）/"
				}
				{"佐倉望"
					DRC(301, 2, EF_SLIDE)
					"[佐倉望:301]通常（1）/"
				}
				{"終了する"
					break
				}
				
				// 立ち絵消去
				ERC(2, EF_SLIDE)
				
				// 顔ウィンドウ消去
				FACE_OFF()
				
				// 話者名消去
				NAME_OFF()
			}
		}
		{"キャラクター"
			"キャラを描画します/"
			DRC(1, 0)
			DRC(101, 1)
			DRC(201, 2)
			
			"キャラをジャンプさせます/"
			CH_JUMP(0) // 1回ジャンプ
			WAIT(6)
			CH_JUMP(1) // 1回ジャンプ
			WAIT(6)
			CH_JUMP(2) // 1回ジャンプ
			
			"キャラを揺らします/"
			CH_SHAKE(0) // 小さい揺れ
			WAIT(6)
			CH_SHAKE(1) // 小さい揺れ
			WAIT(6)
			CH_SHAKE(2) // 小さい揺れ
			
			"画面を揺らします/"
			SHAKE(0.02, 30)
			"画面を揺らしました/"
			
			"キャラをフラッシュします/"
			CH_FLASH(0)
			
			"キャラを消します/"
			ERC(0, EF_ALPHA)
			ERC(1, EF_ALPHA)
			ERC(2, EF_ALPHA)
			"おしまい/"
		}
		{"アイテム表示"
			"演出なし/"
			DRI(1, EF_NONE)
			WAIT(30)
			ERI(EF_NONE)

			"ポップアップ/"
			DRI(1, EF_POPUP)
			WAIT(90)
			ERI(EF_POPUP)

			"半透明表示/"
			DRI(1, EF_ALPHA)
			WAIT(90)
			ERI(EF_ALPHA)

			"スライド表示/"
			DRI(1, EF_SLIDE)
			WAIT(90)
			ERI(EF_SLIDE)

			"おしまい/"

		}
		{"シーケンサーを表示"
			SEQ_DRAW(@"test", 0, 0)
			"シーケンサー表示/"

			SEQ_ERASE()
			"シーケンサーを消去/"
		}
		{"戻る"
			break
		}
	}
	return
}

// -----------------------------------
// サウンド
// -----------------------------------
def sound
{
	while(true) {
		select("サウンドテスト")
		{"ピッチ変更"
			SE_PITCH(@"quiz_good", 1)
			"通常のSE/"
			SE_PITCH(@"quiz_good", 0.5)
			"ピッチ 0.5/"
			SE_PITCH(@"quiz_good", 0.25)
			"ピッチ 0.25/"
			SE_PITCH(@"quiz_good", 2)
			"ピッチ 2/"
			SE_PITCH(@"quiz_good", 4)
			"ピッチ 4/"
		}
		{"戻る"
			break
		}
	}
}

// ---------------------------------------
// その他
// ---------------------------------------
def other
{
	while(true) {
		select("確認する項目を選んでください")
		{"名前入力"
			WND_CLOSE()
			NAME_INPUT(@"名前を入力してください")
			DRC(2, 1)
			"[$girl:1]おはよう、$heroくん/"
			
			CLS()
		}
		{"フェード・フラッシュテスト"
			FADE_OUT(WHITE, 30)
			FADE_WAIT()
			FADE_IN(WHITE, 30)
			FADE_WAIT()
			// フラッシュテスト
			FLASH(WHITE, 60)
			WAIT(60)
			FLASH(WHITE, 60)
			WAIT(60)
		}
		{"パズル"
			WND_CLOSE()
			PUZZLE(0)
		}
		{"戻る"
			break
		}
	}
	return
}

// 演算子テスト
def op
{
	%1 = false
	%2 = true
	%3 = true
	$1 = 0
	$2 = 1
	if(!%1) {
		"%1はONでない/"
	}
	if(%2 && %3) {
		"%2と%3はともにON/"
	}
	if(%1 || %2) {
		"%1または%2がON/"
	}
	if($1 != $2) {
		"v1=$1 / v2=$2"
		"v1とv2は同じでない/"
	}
}

// whileループテスト
def loop
{
	while($1 < 3) {
		$1 += 1
		"ループ回数：$1/"
	}
	while($1 > 0) {
		$1 -= 1
		"カウントダウン：$1/"
	}
	while($1 <= 3) {
		$1 += 1
		"ループ回数：$1/"
	}
	while($1 >= 0) {
		$1 -= 1
		"カウントダウン：$1/"
	}
}

// BG/CH表示テスト
def bg_ch
{
	DRB(1)
	"背景切り替え/"
	DRB(2, EF_ALPHA)
	"背景消去/"
	ERB()
	"キャラを描画します/"
	DRC(1, 0, EF_SLIDE)
	WAIT(6)
	
	"キャラを消します/"
	ERC(0, EF_SLIDE)
	"おしまい/"
}

// 選択肢テスト
def sel
{
	DRB(1)
	select("君は<color.red>十字路<color>に立っている"
		"どちらへ進むべきだろうか？"
	)
	{(%1==false);"直進する"
		"このまま真っ直ぐ進もう/"
		%2 = true
	}
	{(%2);"右に曲がる"
		"右に行ってみるか……/"
		%3 = true
	}
	{(%3);"左に曲がる"
		"左に進んでみよう……/"
	}
}

// シーンジャンプテスト
def jump
{
	"adv001.csv の jump_002 に"
	"シーンジャンプします/"
	JUMP(1, 2)
}

// 変数設定テスト
def val
{
	$1 += 2
	$1 -= 2
	$1 *= 2
	$1 /= 2
}

// フラグ設定テスト
def flag
{
	if(%1) {
		"フラグ1がONです/"
	}
	else {
		"フラグ1がOFFです/"
	}
}

def shake
{
	DRB(1)
	DRC(1, 0)
	"画面を揺らします/"
	SHAKE(0.02, 30)
	"おしまい/"
}
